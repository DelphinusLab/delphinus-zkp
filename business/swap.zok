from "./command" import Command, checkToken, checkAsset, checkAccount, getValueBySelector, checkNonceAndUpdate, checkCommandSign, checkedAdd, checkedSub, checkPool, LEAF_INFOS
from "../utils/markle-tree" import LeafInfo, checkLeafInfo, setAmountBySelector, getPoolToken0Info, getPoolToken1Info, getPoolToken0Amount, getPoolToken1Amount, setAmountAllLeaves, getPoolIndex, addAmount, subAmount, checkEmptyLeafInfo

def main(Command command, field root, LeafInfo[5] leafInfos) -> field:
    field account = command.args[3]
    field pool = command.args[4]
    field amount = command.args[5]
    field direction = command.args[6]
    field nonce = command.args[7]

    for u32 i in 4..LEAF_INFOS do
        assert(checkEmptyLeafInfo(leafInfos[i]))
    endfor

    LeafInfo leafInfo = leafInfos[0]
    assert(checkLeafInfo(root, leafInfo))
    assert(checkAccount(leafInfo, account))
    assert(checkCommandSign(command, leafInfo, [[command.op, account], [pool, amount], [direction, nonce]]))
    root = checkNonceAndUpdate(leafInfo, nonce)

    // change pool data
    leafInfo = leafInfos[1]
    assert(checkLeafInfo(root, leafInfo))
    assert(checkPool(leafInfo, pool))

    field token0Info = getPoolToken0Info(leafInfo)
    field token1Info = getPoolToken1Info(leafInfo)
    field token0Amount = getPoolToken0Amount(leafInfo)
    field token1Amount = getPoolToken1Amount(leafInfo)

    field[4] leaves = if direction == 0 then [token0Info, token1Info, checkedAdd(token0Amount, amount), checkedSub(token1Amount, amount)] else [token0Info, token1Info, checkedSub(token0Amount, amount), checkedAdd(token1Amount, amount)] fi
    root = setAmountAllLeaves(leafInfo, leaves)

    // change token0 balance
    leafInfo = leafInfos[2]
    assert(checkLeafInfo(root, leafInfo))
    assert(checkAccount(leafInfo, account))
    assert(checkAsset(leafInfo, token0Info))
    assert(checkToken(token0Info))
    root = if direction == 0 then subAmount(leafInfo, amount) else addAmount(leafInfo, amount) fi

    // change token1 balance
    leafInfo = leafInfos[3]
    assert(checkLeafInfo(root, leafInfo))
    assert(checkAccount(leafInfo, account))
    assert(checkAsset(leafInfo, token1Info))
    assert(checkToken(token1Info))
    root = if direction == 0 then addAmount(leafInfo, amount) else subAmount(leafInfo, amount) fi

    return root