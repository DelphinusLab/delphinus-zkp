from "./command" import Command, CommandResult, checkTokenRange, checkCommandSign, COMMAND_ARGS, getNonce, setNonce, checkNonceLeafInfoIndex
from "../utils/markle-tree" import LeafInfo, checkBalanceLeafInfoIndex, getValue, setValue, checkPoolLeafInfoIndex, getPoolToken0Info, getPoolToken1Info, getPoolToken0Amount, getPoolToken1Amount, setValues

def main(Command command, LeafInfo[5] leafInfos) -> CommandResult:
    CommandResult cres = CommandResult {
        succeed: true,
        leafInfos: leafInfos
    }
    cres.succeed = true

    field account = command.args[3]
    field pool = command.args[4]
    field amount = command.args[5]
    field direction = command.args[6]
    field nonce = command.args[7]

    // nonce
    LeafInfo leafInfo = leafInfos[0]
    cres.succeed = cres.succeed && checkNonceLeafInfoIndex(leafInfo, account)
    cres.succeed = cres.succeed && checkCommandSign(command, leafInfo, [[command.op, account], [pool, amount], [direction, nonce]])
    cres.succeed = cres.succeed && getNonce(leafInfo) == nonce
    cres.leafInfos[0] = setNonce(leafInfo, nonce + 1)

    // change pool data
    leafInfo = leafInfos[1]
    cres.succeed = cres.succeed && (checkPoolLeafInfoIndex(leafInfo, pool))

    field token0Info = getPoolToken0Info(leafInfo)
    field token1Info = getPoolToken1Info(leafInfo)
    field token0Amount = getPoolToken0Amount(leafInfo)
    field token1Amount = getPoolToken1Amount(leafInfo)

    cres.succeed = cres.succeed && (token0Info != 0)
    cres.succeed = cres.succeed && (token1Info != 0)

    field token0AmountNew = if direction == 0 then token0Amount + amount else token0Amount - amount fi
    cres.succeed = cres.succeed && if direction == 0 then token0Amount < token0AmountNew else token0Amount > token0AmountNew fi

    field token1AmountNew = if direction != 0 then token1Amount + amount else token1Amount - amount fi
    cres.succeed = cres.succeed && if direction != 0 then token1Amount < token1AmountNew else token1Amount > token1AmountNew fi

    field[4] leaves = [token0Info, token1Info, token0AmountNew, token1AmountNew]
    cres.leafInfos[1] = setValues(leafInfo, leaves)

    // change token0 balance
    leafInfo = leafInfos[2]
    cres.succeed = cres.succeed && (checkBalanceLeafInfoIndex(leafInfo, account, token0Info))
    cres.succeed = cres.succeed && (checkTokenRange(token0Info))
    field balance0 = getValue(leafInfo)
    field balance0New = if direction == 0 then balance0 - amount else balance0 + amount fi
    cres.succeed = cres.succeed && if direction == 0 then balance0 > balance0New else balance0 < balance0New fi
    cres.leafInfos[2] = setValue(leafInfo, balance0New)

    // change token1 balance
    leafInfo = leafInfos[3]
    cres.succeed = cres.succeed && (checkBalanceLeafInfoIndex(leafInfo, account, token1Info))
    cres.succeed = cres.succeed && (checkTokenRange(token1Info))
    field balance1 = getValue(leafInfo)
    field balance1New = if direction != 0 then balance1 - amount else balance1 + amount fi
    cres.succeed = cres.succeed && if direction != 0 then balance1 > balance1New else balance1 < balance1New fi
    cres.leafInfos[3] = setValue(leafInfo, balance1New)

    return cres